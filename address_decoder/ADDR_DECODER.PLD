Name     ADDR_DECODER;
PartNo   00;
Date     03.01.2016;
Revision 07;
Designer JM;
Company  Home;
Assembly None;
Location ;
Device   f1502ispplcc44;

/* *************** INPUT PINS *********************/

PIN 2   =  ALE;   /* Address Latch Enable */
PIN [4,5,6,8,9,11,12,14] = [A15..A8];
PIN 16  =  !RD;
PIN 17  =  !WR;

/* *************** OUTPUT PINS *********************/
PIN 39  =  RAM1;
PIN 37  =  RAM2;
PIN 36  =  LCD;
PIN 34  =  N_RD;
PIN 33  =  N_WR;
PIN 31  =  !N_ALE;
PIN 29  =  !ACC;   /* Pin active when RAM work! */

/* ********** LOGIC ********** */

RAM1 = !A15 & (A14 # A13 # A12 # A11 # A10);
N_RD = A15 & A14 & A13 & A12 & A11 & A10 & A9 & A8 & RD; /* Net Read */
N_WR = A15 & A14 & A13 & A12 & A11 & A10 & A9 & A8 & WR; /* Net Write */
LCD  = A15 & A14 & A13 & A12 & A11 & A10 & A9 & !A8 & WR;
RAM2 = A15 & !(A14 & A13 & A12 & A11 & A10 & A9);

N_ALE = ALE; /* The real inversion occurs in the pin declaration */
ACC = WR # RD;

/* From ADDR_DECODER.fit:

               A  A  A  V  A          G
               1  1  1  C  L          N
               3  4  5  C  E          D
            ____________________________________
           /   6  5  4  3  2  1 44 43 42 41 40  \
      TDI |  7                                39 | RAM1
      A12 |  8                                38 | TDO
      A11 |  9                                37 | RAM2
      GND | 10                                36 | LCD
      A10 | 11                                35 | VCC
       A9 | 12            ATF1502             34 | N_RD
      TMS | 13          44-Lead PLCC          33 | N_WR
       A8 | 14                                32 | TCK
      VCC | 15                                31 | N_ALE
       RD | 16                                30 | GND
       WR | 17                                29 | ACC
          |   18 19 20 21 22 23 24 25 26 27 28   |
           \____________________________________/
                          G  V
                          N  C
                          D  C

     A15 A14 A13 A12 A11 A10  A9  A8
     -------------------------------
0x00   0   0   0   0   0   0   0   0  Internal Atmega RAM
0x03   0   0   0   0   0   0   1   1  First 1024 bytes of RAM is Internal RAM
                                       (real: 608 = 512RAM + Reg File + ...)
0x04   0   0   0   0   0   1   0   0  RAM1
0x7F   0   1   1   1   1   1   1   1

0x80   1   0   0   0   0   0   0   0  RAM2
0xFD   1   1   1   1   1   1   0   1

0xFE   1   1   1   1   1   1   1   0  LCD

0xFF   1   1   1   1   1   1   1   1  NET

*/
