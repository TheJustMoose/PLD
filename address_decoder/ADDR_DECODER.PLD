Name     ADDR_DECODER;
PartNo   00;
Date     29.11.2015;
Revision 05;
Designer JM;
Company  Home;
Assembly None;
Location ;
Device   g16v8a;

/* *************** INPUT PINS *********************/

PIN 1   =  ALE;   /* Address Latch Enable */
PIN [2..9] = [A15..A8];
PIN 11  =  !RD;
PIN 12  =  !WR;

/* *************** OUTPUT PINS *********************/
PIN 19  =  !RAM1;
PIN 18  =  !RAM2;
PIN 17  =  LCD;
PIN 16  =  N_RD;
PIN 15  =  N_WR;
PIN 14  =  !N_ALE;
PIN 13  =  !ACC;   /* Pin active when RAM work! */

/* ********** LOGIC ********** */

RAM1 = !A15 & (A14 # A13 # A12 # A11 # A10);
N_RD = A15 & A14 & A13 & A12 & A11 & A10 & A9 & A8 & RD; /* Net Read */
N_WR = A15 & A14 & A13 & A12 & A11 & A10 & A9 & A8 & WR; /* Net Write */
LCD  = A15 & A14 & A13 & A12 & A11 & A10 & A9 & !A8 & WR;
RAM2 = A15 & !(A14 & A13 & A12 & A11 & A10 & A9);

N_ALE = ALE; /* The real inversion occurs in the pin declaration */
ACC = WR # RD;

/*
           ATF16V8B
     ----------  ----------
   -|1   ALE|  \/  |Vcc  20|-
   -|2   A15|      |!R1  19|-
   -|3   A14|      |!R2  19|-
   -|4   A13|      |LCD  17|-
   -|5   A12|      |N_RD 16|-
   -|6   A11|      |N_WR 15|-
   -|7   A10|      |!ALE 14|-
   -|8    A9|      |!ACC 13|-
   -|9    A8|      |!WR  12|-
   -|10  GND|      |!RD  11|-
     ----------------------

     A15 A14 A13 A12 A11 A10  A9  A8
     -------------------------------
0x00   0   0   0   0   0   0   0   0  Internal Atmega RAM
0x03   0   0   0   0   0   0   1   1  First 1024 bytes of RAM is Internal RAM
                                       (real: 608 = 512RAM + Reg File + ...)
0x04   0   0   0   0   0   1   0   0  RAM1
0x7F   0   1   1   1   1   1   1   1

0x80   1   0   0   0   0   0   0   0  RAM2
0xFD   1   1   1   1   1   1   0   1

0xFE   1   1   1   1   1   1   1   0  LCD

0xFF   1   1   1   1   1   1   1   1  NET

*/
